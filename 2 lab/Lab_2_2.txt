






















































T 56 K
G K

[0] A 3 F [пролог: формирование кода инструкции возврата в Acc]
[1] T 63[<ret>] @  [ пролог: запись инструкции возврата ]
[0 F - длина массива]
[1 F - адрес первого элемента массива]

[2] A 64[<c0>] @
[3] T 2[<k>] F
[4] A 0[<l>] F
[5] S 65[<c1>] @
[6] S 65[<c1>] @
[7] U 6[<l1>] F
[8] T 7[<l2>] F

[9] A 1 F [получение адреса первого элемента массива]
[10] A 15[<n>] @
[11] T 15[<n>] @

[12] A 1 F [получение адреса первого элемента массива]
[13] A 19[<n+1>] @
[14] T 19[<n+1>] @

[<loop1>]
[15] A 0[n] F [берем следующее число] [<m1>]
[16] T 3 F [записываем его в рабочую ячейку]
[17] A 65[<c1>] @ [обновляем текущий счетчик]
[18] T 4[<j>] F

[<loop2>]
[19] A 1[n+1] F [сравниваем его с остальными числами в массиве <m2>]

[проверка на совпадение]
[20] S 3[<b>] F [вычитаем из текущего числа число цикла]
[21] G 28[<exit after comparison>] @ [если результат <0, выходим из loop2]
[22] S 65[<c1>] @ [иначе результат больше нуля, вычитаем 1]
[23] E 28[<exit after comparison>] @ [если число совпало, то результат -- -1, продолжаем цикл, иначе выход]
[24] T 300 F [сброс аккумулятора]
[25] A 4[<j>] F [загружаем счетчик текущего числа в аккумулятор]
[26] A 65[<c1>] @ [увеличиваем аккумулятор на 1]
[27] T 4[<j>] F [записываем обновленный счетчик]

[exit after comparison]
[28] T 300 F [сброс аккумулятора]
[29] A 7[<l2>] F [загружаем счетчик для проверки условия выхода]
[30] S 65[<c1>] @ [уменьшаем счетчик l2]
[31] G 38[<continue back 2 <loop1>] @ [проверка условия выхода из loop2]
[32] T 7 F

[меняем инструкции <loop2>]
[33] A 65[<c1>] @
[34] L 0 D
[35] A 19[<m2>] @
[36] T 19[<m2>] @
[37] E 19[<loop2>] @ [следующая итерация loop2]

[continue 2 <loop1> from <loop2>]
[проверка с предыдущем рез-том]
[38] T 300 F [сброс аккумулятора]
[39] A 2[<k>] F [загружаем предыдущий рез]
[40] S 4[<j>] F [предыдущий минус текущий]
[41] E 47[<skip next steps>] @ [если получилось >= 0, текущий результат запоминать не надо, пропускаем следующие шаги]
[42] T 300 F [сброс аккумулятора]
[43] A 4[<j>] F [загружаем текущий результат]
[44] T 2[<k>] F [запоминаем текущий результат]
[45] A 3[<b>] F [загружаем текущее число]
[46] T 1[<a>] F [запоминаем текущее число]

[<return here to skip steps>]
[уменьшаем счетчик <loop1>]
[47] T 300 F [сброс аккумулятора]
[48] A 6[<l1>] F
[49] S 65[<c1>] @ [уменьшаем счетчик <loop1>]
[50] G 63[<ret>] @ [проверка условия для выхода из <loop1>]
[51] T 6[<l1>] F [запоминаем счетчик <loop1>]

[меняем инструкции <loop1>]
[52] A 65[<c1>] @
[53] L 0 D
[54] A 15[<m1>] @
[55] T 15[<m1>] @

[меняем инструкции <loop2>]
[56] A 65[<c1>] @
[57] L 0 D
[58] A 15[<m1>] @
[59] T 19[<m2>] @

[обновляем счетчик <loop2>]
[60] A 6[<l1>] F
[61] T 7[<l2>] F
[62] E 15[<loop1>] @ [возврат в <loop1>]
[63] E 0 F [<ret>]  [ эпилог: инструкция возврата из подпрограммы ]
[64] P 0 F [<c0>, const = 0]
[65] P 0 D [<c1>, const = 1]

[--------------------------]

G K
[0] X F
[1] A 9[<l>] @
[2] T 0 F [длину массива в ячейку 0 для передачи подпрограмме]
[3] A 8[<first>] @
[4] T 1 F [адрес первого элемента массива в ячейку 1 для передачи подпрограмме]
[5] A 5[<точка вызова>] @ [вызов подпрограммы]
[6] G 56 F [вызов подпрограммы]

[7] Z 0 F [выход]

[8] P 10[<first>] @ [<first>, адрес первого элемента массива]
[9] P 5 F [<l>, длина массива = 10]

[<массив>]
[10] P 9 F [18] [<first>]
[11] P 4 F [8]
[12] P 5 D [11]
[13] P 2 D [5]
[14] P 89 F [178]
[15] P 89 F [178]
[16] P 89 F [178]
[17] P 89 F [178]
[18] P 1 F [2]
[19] P 89 F [178]
EZPF [директива IO2, переход к исполнению]

